apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 29
//    buildToolsVersion "30.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    api fileTree(dir: "libs", include: ["*.jar"])
    compileOnly 'com.appsflyer:af-android-sdk:6.0.0'
    compileOnly 'com.google.firebase:firebase-analytics:17.4.4'
    compileOnly 'com.facebook.android:facebook-android-sdk:7.1.0'

    compileOnly 'com.sayweee.libs:logger:0.0.3'
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar
}


uploadArchives {
    configuration = configurations.archives
    repositories {
        mavenDeployer {
            if (UPLOAD_TRACK_MAVEN.toBoolean()) {
                snapshotRepository(url: config.MAVEN_REPO_SNAPSHOT_URL) {
                    authentication(userName: config.NEXUS_USERNAME, password: config.NEXUS_PASSWORD)
                }
                repository(url: config.MAVEN_REPO_RELEASE_URL) {
                    authentication(userName: config.NEXUS_USERNAME, password: config.NEXUS_PASSWORD)
                }
                pom.project {
                    version '0.0.3'
                    artifactId 'track'
                    groupId GROUP_ID
                    packaging TYPE
                    description DESCRIPTION
                }
            } else {
                repository(url: mavenLocal().getUrl())
                snapshotRepository(url: mavenLocal().getUrl())
                pom.project {
                    version '0.0.2-SNAPSHOT'
                    artifactId 'track'
                    groupId GROUP_ID
                    packaging TYPE
                    description DESCRIPTION
                }
            }
        }
    }
}

